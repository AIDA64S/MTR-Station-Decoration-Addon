import org.apache.tools.ant.filters.ReplaceTokens
import org.mtr.mod.BuildTools

plugins {
	id "net.minecraftforge.gradle" version "+"
	id "org.spongepowered.mixin" version "+"
}

final BuildTools buildTools = new BuildTools(minecraftVersion, "forge", project)

minecraft {
	mappings channel: "official", version: minecraftVersion
	accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")
	runs {
		configureEach {
			property "forge.logging.markers", "REGISTRIES"
			property "forge.logging.console.level", "debug"
		}
		client {}
		server { args "--nogui" }
	}
}

sourceSets.main.resources { srcDir "src/generated/resources" }

dependencies {
	minecraft "net.minecraftforge:forge:${minecraftVersion}-${buildTools.getForgeVersion()}"
	annotationProcessor "org.spongepowered:mixin:+:processor"
	implementation "org.mtr:MTR-forge-${minecraftVersion}:${mtrVersion}-dev"
}

mixin {
	add sourceSets.main, "msd.refmap.json"
	config "msd.mixins.json"
	debug.verbose = true
	debug.export = true
}

java {
	toolchain {
		languageVersion.set(JavaLanguageVersion.of(buildTools.javaLanguageVersion))
	}
	withSourcesJar()
	withJavadocJar()
}

tasks.register("setupFiles") {
	copy {
		outputs.upToDateWhen { false }
		from "src/main/mods.template.toml"
		into "src/main/resources/META-INF"
		filter(ReplaceTokens, tokens: ["minecraft": minecraftVersion, "version": minecraftVersion + "-" + version])
		rename "(.+).template.toml", "\$1.toml"
	}

	delete fileTree("src/main/java/top/mcmtr/mod")
	delete fileTree("src/main/resources/assets")

	copy {
		outputs.upToDateWhen { false }
		from "../fabric/src/main/java/top/mcmtr/mod"
		into "src/main/java/top/mcmtr/mod"
	}

	copy {
		outputs.upToDateWhen { false }
		from "../fabric/src/main/java/top/mcmtr/core"
		into "src/main/java/top/mcmtr/core"
	}

	copy {
		outputs.upToDateWhen { false }
		from "../fabric/src/main/resources/assets"
		into "src/main/resources/assets"
	}
}

build {
	doLast {
		buildTools.copyBuildFile()
	}
}

shadowJar {
	finalizedBy "reobfShadowJar"
}

reobf {
	shadowJar {}
}

assemble.dependsOn shadowJar
